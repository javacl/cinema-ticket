plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "com.google.devtools.ksp"
    id "dagger.hilt.android.plugin"
}

private Integer generateVersionCode() {
    return project.minimumSdkVersion * 10000000 + project.versionMajor * 10000 + project.versionMinor * 100 + project.versionPatch
}

private String generateVersionName() {
    return "${project.versionMajor}.${project.versionMinor}.${project.versionPatch}"
}

project.ext {
    versionMajor = 0
    versionMinor = 9
    versionPatch = 0
    minimumSdkVersion = 24
    compileAndTargetSdkVersion = 34
    appName = "sample.cinema.ticket"
}

android {

    namespace project.appName
    compileSdk project.compileAndTargetSdkVersion

    defaultConfig {
        applicationId project.appName
        minSdk project.minimumSdkVersion
        targetSdk project.compileAndTargetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        multiDexEnabled = true
        resConfigs "en"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.3"
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "cinema-ticket-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {

    // Compose
    def composeBom = platform("androidx.compose:compose-bom:2023.10.01")
    implementation composeBom
    androidTestImplementation composeBom
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.ui:ui-util"

    implementation "androidx.activity:activity-compose:1.8.0"
    implementation "androidx.navigation:navigation-compose:2.7.4"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Architecture Components
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.2"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Okhttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:5.0.0-alpha.11"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    // Moshi Kotlin
    def moshi_version = "1.15.0"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Coroutine
    def coroutines_version = "1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Coil
    def coil_version = "2.4.0"
    implementation "io.coil-kt:coil-compose:$coil_version"

    // KTX
    implementation "androidx.core:core-ktx:1.12.0"

    // Multidex
    implementation "androidx.multidex:multidex:2.0.1"

    // Splash
    implementation "androidx.core:core-splashscreen:1.0.1"

    // Test
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
}
